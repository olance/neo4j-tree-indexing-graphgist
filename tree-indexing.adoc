= In-graph tree indexing

Storing tree structures in a graph database makes a lot of sense. However, some trees are easier to play with than others.
Some constraints will make things much more complicated. Ordered children for instance, or time-dependent hierarchies (a tree 
that changes in time), or ordered time-dependent hierarchies...

In this GraphGist, I will present tree indexing structures that make it easier to deal with that kind of trees and that would ultimately allow:

* Returning the full hierarchy with a single query, in a depth-first traversal fashion that allows building back the 
hierarchy in memory using your client language.
* Similarly, extracting the full subtree from a given node, with a single query.
* List direct children of any given node within the tree.

All of this for arbitrary depths of trees and with preservation of children order at each level in the returned data!

Please note that this document is as much a presentation as a personal research on in-graph tree indexing. It shows how I, as a graph/Neo4j neophyte, started from a problem with working on trees and ended up at something that looked good enough for me. If you know better than me, which I'm sure most of you do, please chime in in the comments and let's discuss :)

== Let's start with a tree

Here's the tree we'll be working with:

//hide
//setup
[source,cypher]
----
CREATE (root:CATEGORIES {name: '1. Motorcycle Racing Categories'})

CREATE (root)-[:HAS]->(road:CATEGORY {name: '1.1 Road Racing'})
CREATE (road)-[:HAS]->(gp:SUBCATEGORY {name: '1.1.1 Grand Prix'})
CREATE (gp)-[:HAS]->(moto2:CLASS {name: '1.1.1.2 Moto2'})
CREATE (gp)-[:HAS]->(moto3:CLASS {name: '1.1.1.1 Moto3'})
CREATE (gp)-[:HAS]->(motogp:CLASS {name: '1.1.1.3 MotoGP'})

CREATE (road)-[:HAS]->(tradi:SUBCATEGORY {name: '1.1.6 Traditional'})
CREATE (road)-[:HAS]->(ssp:SUBCATEGORY {name: '1.1.3 Supersport'})
CREATE (road)-[:HAS]->(sbk:SUBCATEGORY {name: '1.1.2 Superbike'})
CREATE (road)-[:HAS]->(sidecar:SUBCATEGORY {name: '1.1.5 Sidecar'})
CREATE (road)-[:HAS]->(endurance:SUBCATEGORY {name: '1.1.4 Endurance'})

CREATE (root)-[:HAS]->(cross:CATEGORY {name: '1.2 Motocross'})
CREATE (cross)-[:HAS]->(supermoto:SUBCATEGORY {name: '1.2.2 Supermoto'})
CREATE (cross)-[:HAS]->(supercross:SUBCATEGORY {name: '1.2.1 Supercross'})

RETURN *
----

_Note: Nodes have been created in random order to ensure subsequent queries do not return ordered nodes "by accident"_

// graph

As you probably can see, order is important in this tree. If we wanted to display the complete hierarchy of motorcycle racing categories as a plan for an article (just as it's done in http://en.wikipedia.org/wiki/Motorcycle_racing#toctitle[this wikipedia article] the data's been taken from), we'd probably love to query the whole tree and have nothing more to do than display it, confident that items will be in correct order.

== Intuitive approach

The problem with such a graph is that all the order information is lost: the `:HAS` relationships do not allow us to retrieve the children of a node in order. +
What about storing the position  of each child in its `:HAS` relationship? Something like this:

[source,cypher]
----
// Match all :HAS relationships and the child they point to
MATCH ()-[r:HAS]->(child)

// 1. Split child name by spaces: "1.1.1.3 MotoGP" => ["1.1.1.3", "MotoGP"]
// 2. Split first item by dots and take last: "1.1.1.3" => "3"
// 3. Convert to Int and store in relationship's `pos` property
SET r.pos = toInt(last(split(head(split(child.name, ' ')), '.')))
RETURN r
----

// table

_Note: this query works here because we had the position in each node's label, you would of course need to come up with your own algorithm to build your tree with the correct `pos` property on each relationship_

Now that we have this position property on each relationship, we can effectively get the ordered children of a given node:

[source,cypher]
----
MATCH (root {name: '1.1.1 Grand Prix'})-[r:HAS]->(child)
RETURN child
ORDER BY r.pos
----

// table

What if we want to get the whole tree? We can use a variable-length relationships syntax to do that:

[source,cypher]
----
MATCH p = (root:CATEGORIES)-[_rels:HAS*0..]->(child)
WITH [r in _rels|r.pos] AS positions, [n in nodes(p)|n.name] AS paths
RETURN paths, positions
----

// table


The problem is that we do get the complete tree as a result of that kind of query, but it's going to be very difficult to deal with the relationships collections represented by `_rels` and the paths represented by `p` in the query above to return a list of nodes that can be easily used to build back the tree later on (I'm assuming we want to do as few operations as possible on the client side). +
The cause of this problem seems to be that our tree is represented by multiple paths: one for each leaf, with paths to nodes being sub-paths of those. This leaves us with handling collections of paths and relationships which cannot be easily ordered back to a list of node, as the order information lies in the relationships. "Not very graphy", as some would say!

No matter how hard I tried, I never found a way to get a useful result out of this graph and my Cypher queries were beginning to be way too complicated. Conclusion: the graph structure has too change!


== The "DFS path" model

=== Some theory
Instead of modeling our data the way we're used to see it, let's look at it the other way round and model it according to the way we want to query it/get it back. +
One simple way to flatten a tree so that it can be built back later is to list its nodes with a pre-order depth-first search algorithm (DFS). Here's a quick overview of this algorithm for those who are not familiar with it:

1. Select the root node
2. Select the first child node and repeat until there's no child node to go to
3. If the current node has unselected siblings: Select the next sibling and go to 2.
4. If the current node has no unselected siblings: Go back to the node's parent and go to 3.
5. Output each node when it's first selected

An animation will probably help you grasp the idea:

image::http://upload.wikimedia.org/wikipedia/commons/7/7f/Depth-First-Search.gif[]
(By Mre (Own work) http://creativecommons.org/licenses/by-sa/3.0[CC BY-SA 3.0], via Wikimedia Commons)

If we apply this algorithm to our own tree, we'd get each node in this order:

----
1. Motorcycle Racing Categories (:CATEGORIES)
1.1 Road Racing (:CATEGORY)
1.1.1 Grand Prix (:SUBCATEGORY)
1.1.1.1 Moto3 (:CLASS)
1.1.1.2 Moto2 (:CLASS)
1.1.1.3 MotoGP (:CLASS)
1.1.2 Superbike (:SUBCATEGORY)
1.1.3 Supersport (:SUBCATEGORY)
1.1.4 Endurance (:SUBCATEGORY)
1.1.5 Sidecar (:SUBCATEGORY)
1.1.6 Traditional (:SUBCATEGORY)
1.2 Motocross (:CATEGORY)
1.2.1 Supercross (:SUBCATEGORY)
1.2.2 Supermoto (:SUBCATEGORY)
----

What's great about this list, is that it is pretty easy to use to build back the tree in memory with a very simple _O(n)_ algorithm using a stack.

=== Applying this algorithm to our graph
The previous result shows us there is a way to represent our tree with a *single* path. The only thing we have to do is remove all our `:HAS` relationships and instead create a relationship between each consecutive node in the order defined above. +
The next two queries will do just that. Of course in a real-world application, you would directly create the graph from your tree by traversing it with the depth-first algorithm and creating each node/relationship on the fly.

// hide
[source,cypher]
----
// Delete :HAS relationships
MATCH ()-[r:HAS]-()
DELETE r
----

[source,cypher]
----
// Create pre-ordered depth-first path

// Match all nodes individually so we can name them
MATCH (root:CATEGORIES)

MATCH (road:CATEGORY {name: '1.1 Road Racing'})
MATCH (cross:CATEGORY {name: '1.2 Motocross'})

MATCH (gp:SUBCATEGORY {name: '1.1.1 Grand Prix'})
MATCH (sbk:SUBCATEGORY {name: '1.1.2 Superbike'})
MATCH (ssp:SUBCATEGORY {name: '1.1.3 Supersport'})
MATCH (endurance:SUBCATEGORY {name: '1.1.4 Endurance'})
MATCH (sidecar:SUBCATEGORY {name: '1.1.5 Sidecar'})
MATCH (tradi:SUBCATEGORY {name: '1.1.6 Traditional'})
MATCH (spcross:SUBCATEGORY {name: '1.2.1 Supercross'})
MATCH (spmoto:SUBCATEGORY {name: '1.2.2 Supermoto'})

MATCH (moto3:CLASS {name: '1.1.1.1 Moto3'})
MATCH (moto2:CLASS {name: '1.1.1.2 Moto2'})
MATCH (motogp:CLASS {name: '1.1.1.3 MotoGP'})

WITH *

// Create the path
CREATE (root)-[:NEXT]->(road)-[:NEXT]->(gp)-[:NEXT]->(moto3)
CREATE (moto3)-[:NEXT]->(moto2)-[:NEXT]->(motogp)-[:NEXT]->(sbk)
CREATE (sbk)-[:NEXT]->(ssp)-[:NEXT]->(endurance)-[:NEXT]->(sidecar)
CREATE (sidecar)-[:NEXT]->(tradi)-[:NEXT]->(cross)
CREATE (cross)-[:NEXT]->(spcross)-[:NEXT]->(spmoto)

RETURN *
----

// graph

The graph above doesn't look like a tree anymore... but our tree's still here, it's just organized according to the pre-order DFS algorithm:

image::https://raw.githubusercontent.com/olance/neo4j-tree-indexing-graphgist/master/images/dfs-path.png[]

Getting the whole ordered tree is now as easy as:

[source,cypher]
----
// Get the whole ordered tree
MATCH (root:CATEGORIES)-[:NEXT*0..]->(item)
RETURN item
----

// graph

// table

This query is very simple: we start from the root and return each item that is located at 0 to _n_ `:NEXT` relationships from it. The item at 0 hops from the root is the root itself, so we do get the complete path in the correct order.

To make this query a bit safer, we can order the paths Neo4j will find before returning the items they contain. Although it seems to always be the case, we're indeed not sure that Neo4j will expand our `-[:NEXT*0..]->` relationship by extending the path one node by one node. It may well start from the longest path and decrease to the shortest, so in order to prevent from any unexpected result, here's what we can do:

[source,cypher]
----
// Get the whole ordered tree
MATCH p = (root:CATEGORIES)-[:NEXT*0..]->(item)
WITH p, item
ORDER BY length(p)
RETURN item
----

The addition does not complicate the query too much: we just assign each found path to the `p` variable and sort the items according to the lengths of the paths that led to them.

Now how would we extract a subtree from this tree? We could just start from the root node of the subtree we're interested in, and traverse our path down to the last node that is part of this subtree. +
This is however not possible as it is: we have no way to know when to stop!

To fix this, we can add a relationship between a non-leaf node and the last node in its subtree. Thus, from any given node, we could match its last subtree node and walk the DFS path no further than this marker node. +
Let's add `:LAST` relationships between the appropriate nodes of our tree:

// hide
[source,cypher]
----
// Adding :LAST relationships

// Match "sub-root" nodes
MATCH (road {name: '1.1 Road Racing'})
MATCH (gp {name: '1.1.1 Grand Prix'})
MATCH (cross {name: '1.2 Motocross'})

// Match the last sub-tree nodes of these sub-roots
MATCH (tradi {name: '1.1.6 Traditional'})
MATCH (motogp {name: '1.1.1.3 MotoGP'})
MATCH (spmoto {name: '1.2.2 Supermoto'})

WITH *

CREATE (road)-[:LAST]->(tradi)
CREATE (gp)-[:LAST]->(motogp)
CREATE (cross)-[:LAST]->(spmoto)

WITH spmoto

MATCH p = (spmoto)<-[:NEXT*]-()
RETURN p
----

// graph

Here's a view of the above graph with nodes organized more neatly:

image::https://raw.githubusercontent.com/olance/neo4j-tree-indexing-graphgist/master/images/dfs-path-with-last.png[]

Note that I haven't added a `:LAST` relationship on the tree's root, as there is no sense extracting the tree's "subtree" from the tree's own root. +
Being able to identify the last node of each subtree, we can now extract them quite easily:

[source,cypher]
----
// Get a complete subtree
MATCH (subroot {name: '1.1 Road Racing'})-[:LAST*0..]->(lastNode)
MATCH p = (subroot)-[:NEXT*0..]->(lastNode)

WITH nodes(p) AS n UNWIND n AS nodes

RETURN DISTINCT nodes
----

// table

The query first matches the subroot we're interested in and the last node of its subtree; then, the path from the subroot to this last node is fetched in `p` and we return the unwinded collection of nodes in this path. Both relationships matchers use the `*0..` variable-length qualifier to make sure we return at least the selected subroot when it is a leaf (and hence has no subtree). +
Because of the "0" in this qualifier, the subroot is matched twice, which is why I used the `DISTINCT` clause to avoid duplication in our result set.

The last piece of data we'd like to query is the ordered list of direct children of any given node in the tree. Once again, we'll have to complement our model to make this possible: all nodes in our DFS path are potentially interspersed with the children of their siblings.

My proposition to solve this last problem is to add relationships between sibling nodes to create an ordered linked list from the first to the last sibling of each level.

// hide
[source,cypher]
----
// Add :NEXT_SIBLING rels
MATCH (road {name: '1.1 Road Racing'})
MATCH (cross {name: '1.2 Motocross'})

MATCH (gp {name: '1.1.1 Grand Prix'})
MATCH (sbk {name: '1.1.2 Superbike'})
MATCH (ssp {name: '1.1.3 Supersport'})
MATCH (endurance {name: '1.1.4 Endurance'})
MATCH (sidecar {name: '1.1.5 Sidecar'})
MATCH (tradi {name: '1.1.6 Traditional'})

MATCH (spcross {name: '1.2.1 Supercross'})
MATCH (spmoto {name: '1.2.2 Supermoto'})

MATCH (moto3 {name: '1.1.1.1 Moto3'})
MATCH (moto2 {name: '1.1.1.2 Moto2'})
MATCH (motogp {name: '1.1.1.3 MotoGP'})

WITH *

CREATE (road)-[:NEXT_SIBLING]->(cross)

CREATE (gp)-[:NEXT_SIBLING]->(sbk)-[:NEXT_SIBLING]->(ssp)-[:NEXT_SIBLING]->(endurance)
CREATE (endurance)-[:NEXT_SIBLING]->(sidecar)-[:NEXT_SIBLING]->(tradi)

CREATE (spcross)-[:NEXT_SIBLING]->(spmoto)

CREATE (moto3)-[:NEXT_SIBLING]->(moto2)-[:NEXT_SIBLING]->(motogp)

WITH spmoto

MATCH p = (spmoto)<-[:NEXT*]-()
RETURN p
----

// graph

Once again, here's a screenshot showing the nodes properly laid out:

image::https://raw.githubusercontent.com/olance/neo4j-tree-indexing-graphgist/master/images/dfs-path-complete.png[]

With the `:NEXT_SIBLING` relationships present, getting the direct children of a node remains somewhat tricky. One would be tempted to get the `:NEXT` node of the parent node and then all the `:NEXT_SIBLING` nodes. This will work as long as you are not trying to get the children of a leaf. +
Using this method to get the (non-existing) children of the node "1.1.1.3 MotoGP" would select "1.1.2 Superbike" as the `:NEXT` node and then all its remaining siblings, effectively returning nodes from a higher level than the one we were interested in. +
We have to be more clever than that. Fortunately this is still accessible to the bare mortals we are: we just have to make sure there's no inbound `:NEXT_SIBLING` relationship on the `:NEXT` node we match from our parent node. If there's one, it means the matched node is a sibling of the starting node or a sibling of one of its ancestors. Another way to put this is to say that in a DFS path, a node will never point back at one of its ancestors, so it necessarily points to:

* its first child. No inbound `:NEXT_SIBLING` relationship then (eg. `('1.1')-[:NEXT]->('1.1.1')`).
* or a sibling, to which it'll also point via a `:NEXT_SIBLING` relationship (eg. `('1.1.1.1')-[:NEXT]->('1.1.1.2')`).
* or a sibling of an ancestor node, which will then have an inbound `:NEXT_SIBLING` relationship from the said ancestor (eg. `('1.1.1.3')-[:NEXT]->('1.1.2')`).

Now that we're aware of this special case, let's see how we can query the children of a node:

[source,cypher]
----
// Get direct children of a node

// Match the node we're interested in and its (possible) first child
MATCH ({name: '1.1.1 Grand Prix'})-[:NEXT]->(firstChild)

// Make sure this is really a child
WHERE NOT ()-[:NEXT_SIBLING]->(firstChild)

// Match and return all siblings (including first child)
MATCH (firstChild)-[:NEXT_SIBLING*0..]->(siblings)
RETURN siblings AS children
----

The `WHERE NOT ()-[:NEXT_SIBLING]->(firstChild)` clause is our clever part: it makes sure the `firstChild` node we have matched has no inbound `:NEXT_SIBLING` relationship. If it doesn't, the next `MATCH` will go through the siblings path and return everything in order:

// table

Running the same query with a leaf, we can check that no node is returned:

// hide
[source,cypher]
----
MATCH ({name: '1.1.1.3 MotoGP'})-[:NEXT]->(firstChild)
WHERE NOT ()-[:NEXT_SIBLING]->(firstChild)
MATCH (firstChild)-[:NEXT_SIBLING*0..]->(siblings)
RETURN siblings AS children
----

// table

There's a final problem to this model: it will not work with trees of arbitrary depth. Returning the list of nodes in depth-first search order works only when you're able to distinguish a lower-level node from a higher-level node, so that when iterating on the list of nodes, you can decide whether the current node is a sibling of the previous one, its first child or a sibling of its parent. +
What we've seen so far works with our example because we have a different node label (`CATEGORIES`, `CATEGORY`, `SUBCATEGORY`, `CLASS`) for each tree level. This situation is probably quite common: books, essays, company organisation charts, product categories, (...) can usually be represented with a limited set of labels.

In other cases though, you might not know the depth of your tree in advance and it might not make sense to have a different label for each level. We then need to add a bit of information to our output: the depth of each node.

=== Adding depths

A simple solution to add the depth information to our returned list of nodes, is simply to have it in the properties of each node. There, done!

However in a time-dependent tree, a node could be used at different depths depending on the time reference you use to look at the tree. Having a `depth` property on the node is then impossible. +
Here is a query that computes the depth of each returned node in the tree:

[source,cypher]
----
// Get the whole ordered tree with depths
MATCH p = (root:CATEGORIES)-[:NEXT*0..]->(item)
WITH p, item
ORDER BY length(p)

MATCH (root:CATEGORIES), sp = shortestPath(root-[:NEXT|:NEXT_SIBLING*0..]->item)
WITH item, filter(node IN nodes(sp) WHERE NOT ()-[:NEXT_SIBLING]->(node)) AS levels

RETURN item, length(levels) - 1 AS depth
----

This query is an enhancement of our previous query that gets the whole ordered tree, only for each node it computes the shortest path from the tree's root to the node, traversing through `:NEXT_SIBLING` relationships when possible. +
In this shortest path, we know each traversed level has no inbound `:NEXT_SIBLING` relationship so we count those and subtract `1` as we consider the root to be at depth 0. +
Here's the result:

// table

Depending on how you handle your time-dependent tree (a subject for another Gist?), you'd had to add conditions to traverse only `:NEXT`/`:NEXT_SIBLING` relationships that make sense at the chosen time reference.

Although I haven't done any benchmark, I suspect this query to be quite costly so it might not work for very large trees. Denormalizing the node's depth in its `:NEXT`/`:NEXT_SIBLING` relationships might be a solution?
